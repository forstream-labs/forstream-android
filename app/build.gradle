apply plugin: 'com.android.application'

android {
  compileSdkVersion 29
  defaultConfig {
    applicationId 'io.forstream'
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 5
    versionName '1.0.4'
    multiDexEnabled true
    manifestPlaceholders = [
      'appAuthRedirectScheme': 'io.forstream.api'
    ]
  }

  flavorDimensions 'version'
  productFlavors {
    prod {
      dimension 'version'
      resValue 'string', 'app_name', 'Forstream'
      resValue 'string', 'facebook_app_id', '2605017349737935'
      resValue 'string', 'facebook_login_protocol_scheme', 'fb2605017349737935'
    }
    dev {
      dimension 'version'
      applicationIdSuffix '.dev'
      resValue 'string', 'app_name', 'Forstream DEV'
      resValue 'string', 'facebook_app_id', '276919856844001'
      resValue 'string', 'facebook_login_protocol_scheme', 'fb276919856844001'
    }
  }

  signingConfigs {
    release {
      String keystoreDirectory = "${System.env.KEYSTORES_DIR}/forstream"
      if (file(keystoreDirectory).exists()) {
        File secureProperties = file(keystoreDirectory.concat('/secure.properties'))
        Properties properties = new Properties()
        properties.load(new FileInputStream(secureProperties))
        storeFile file(keystoreDirectory.concat('/release.keystore'))
        storePassword properties.getProperty('signing.storePassword')
        keyAlias properties.getProperty('signing.keyAlias')
        keyPassword properties.getProperty('signing.keyPassword')
      }
    }
    debug {
      storeFile rootProject.file('keystores/debug.keystore')
      storePassword 'android'
      keyAlias 'androiddebugkey'
      keyPassword 'android'
    }
  }

  buildTypes {
    release {
      debuggable false
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
    debug {
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.debug
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  implementation 'com.github.pedroSG94:encoder-release:1.8.3@aar'
  implementation 'com.github.pedroSG94:rtplibrary-release:1.8.3@aar'
  implementation 'com.github.pedroSG94:rtmp-release:1.8.3@aar'

  // Api and exposed dependencies
  implementation project(':api')
  implementation 'com.onehilltech.promises:promises-jvm:0.10.3'
  implementation 'com.onehilltech.promises:promises-android:0.10.3'
  // Android X and support libraries
  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'androidx.cardview:cardview:1.0.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta7'
  implementation 'androidx.core:core:1.3.0'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
  implementation 'androidx.preference:preference:1.1.1'
  implementation 'androidx.recyclerview:recyclerview:1.1.0'
  implementation 'com.android.support:multidex:1.0.3'
  // Google
  implementation 'com.google.android.gms:play-services-auth:18.0.0'
  implementation 'com.google.android.material:material:1.3.0-alpha01'
  implementation 'com.google.dagger:dagger-android:2.27'
  implementation 'com.google.dagger:dagger-android-support:2.27'
  implementation 'com.google.firebase:firebase-analytics:17.4.3'
  implementation 'com.google.firebase:firebase-core:17.4.3'
  implementation 'com.google.firebase:firebase-crashlytics:17.1.0'
  // Facebook
  implementation 'com.facebook.android:facebook-android-sdk:7.0.0'
  implementation 'com.facebook.android:facebook-login:7.0.0'
  // Others
  implementation 'com.annimon:stream:1.2.1'
  implementation 'com.github.bumptech.glide:glide:4.11.0'
//  implementation 'com.github.pedroSG94.rtmp-rtsp-stream-client-java:rtplibrary:1.8.3'
  implementation 'com.github.yalantis:ucrop:2.2.4'
  implementation 'com.jakewharton:butterknife:10.2.1'
  implementation 'com.jakewharton.timber:timber:4.7.1'
  implementation 'com.kroegerama:bottomsheet-imagepicker:1.0.3'
  implementation 'com.mikepenz:fontawesome-typeface:5.9.0.0-kotlin@aar'
  implementation 'com.mikepenz:iconics-core:4.0.2'
  implementation 'com.mikepenz:iconics-views:4.0.2'
  implementation 'com.mikhaellopez:circularimageview:3.2.0'
  implementation 'net.openid:appauth:0.7.1'
  implementation 'org.permissionsdispatcher:permissionsdispatcher:4.3.1'
  // Annotation processors
  annotationProcessor 'com.google.dagger:dagger-android-processor:2.27'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.27'
  annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
  annotationProcessor 'org.permissionsdispatcher:permissionsdispatcher-processor:4.3.1'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
